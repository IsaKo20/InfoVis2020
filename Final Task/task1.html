<html>
    <head>
	<title>FinalTask: Task1</title>
    </head>
    <h2> Overview </h2>
    最終課題としてTask1に取り組んだ。
    UIの実装はカラーマップとリフレクションモデルのそれぞれが選択できるようにした。
    選択肢の前にあるボタンを推す度に切り替わるようになっている。
    <body style="margin:0">
  <script src="three.min.js"></script>
	<script src="TrackballControls.js"></script>
  <script src="http://threejs.org/examples/js/math/Lut.js"></script>
	<script src="https://naohisas.github.io/KVS.js/Build/KVS.min.js"></script>
	<script src="https://naohisas.github.io/KVS.js/Build/KVS2THREE.min.js"></script>
	<script src="https://naohisas.github.io/KVS.js/Source/KVSLobsterData.js"></script>
  <h2> Visualization Results </h2>
  下図に示す通りカラーマップはrainbow、redwhite、grayscaleの3種類、
  リフレクションモデルはlambert、phong、cooktrranceの3種類とした。
  一目で選択肢が分かるようにボタンのタイプをラジオボタンにした。
  <div id="display" style="width: 70%; height: 40%; float: left;"></div>
  <div id="controller" style="width: 30%; height: 40%; float:left;">
       <label id="label" style="font-family:Arial;"></label>
       <input type="range" min="0" max="1" step="0.1" value="0.5"
  id="isovalue"/>
       <h4>Color Map</h4>
       <input id="rainbow" type="radio" name="colormap" value="RAINBOW" onchange="ColorMapClick()" checked> Rainbow
       <br>
       <input id="redwhite" type="radio" name="colormap" value="REDWHITE" onchange="ColorMapClick()"> RedWhite
       <br>
       <input id="gray" type="radio" name="colormap" value="GRAY" onchange="ColorMapClick()"> GrayScale

       <h4>Reflection Model</h4>
       <input id="lambert" type="radio" name="reflection" value="LAMBERT" onchange="ReflectionClick()" > Lambert
       <br>
       <input id="phong" type="radio" name="reflection" value="PHONG" onchange="ReflectionClick()"> Phong
       <br>
       <input id="cooktrrance" type="radio" name="reflection" value="COOKTRRANCE" onchange="ReflectionClick()" checked> CookTrrance
       <br>
        <!-- <button id="apply" style="font-family=Arial;" onclick = "e()">
  Apply</button> -->
  </div>
  <br>
　<h2> Summary </h2>
  カラーマップとリフレクションモデルの原理、可視化の仕方について理解できるようになった。
  ただし、レインボーで、ざりがに全体が授業で扱った三角形のように虹色にしたかったがその方法が分からなかった。
  </script>
  <script type="x-shader/x-vertex" id="phong.vert">
	 varying vec4 point_position;
	 varying vec3 normal_vector;

	 void main()
	 {
	     point_position = modelViewMatrix * vec4( position, 1.0 );
	     normal_vector = normalMatrix * normal;

	     gl_Position = projectionMatrix * point_position;
	 }
	</script>

	<script type="x-shader/x-fragment" id="phong.frag">
	 varying vec4 point_position;
	 varying vec3 normal_vector;
	 uniform vec3 point_color;
	 uniform vec3 light_position;
	 uniform vec3 camera_position;
	 uniform int reflection_model;

	 vec3 LambertianReflection( vec3 C, vec3 L, vec3 N )
	 {
	     float ka = 0.3;
	     float kd = 0.5;

	     float dd = dot( N, L );
	     float Ia = ka;
	     float Id = kd * dd;
       return C * ( Ia + Id );
	 }

	 vec3 PhongReflection( vec3 C, vec3 L, vec3 N, vec3 V )
	 {
	     float ka = 0.3;
	     float kd = 0.5;
	     float ks = 0.8;
	     float n = 50.0;

	     vec3 R = reflect( -L, N );
	     float dd = dot( N, L );
	     float ds = 0.0;
	     if ( dd > 0.0 )
	     {
		 ds = pow( dot( R, V ), n );
	     }

	     float Ia = ka;
	     float Id = kd * dd;
	     float Is = ks * ds;
	     return C * ( Ia + Id + Is );
	 }

	 vec3 CookTorranceReflection( vec3 C, vec3 L, vec3 N, vec3 V )
	 {
	     float ka = 0.3;
	     float kd = 0.5;
	     float ks = 0.8;
	     float m = 0.2; // roughness. [0:smooth, 1:rough]
	     float F0 = 0.2;

	     float dd = dot( N, L );
	     float ds = 0.0;
	     if ( dd > 0.0 )
	     {
		 vec3 H = normalize( L + V );
		 float NL = dd;
		 float NH = dot( N, H );
		 float NV = dot( N, V );
		 float VH = dot( V, H );

		 // Beckmann distribution
		 float NH2 = NH * NH;
		 float NH4 = NH2 * NH2;
		 float D = ( 1.0 / ( 4.0 * m * m * NH4 ) ) * exp( ( NH2 - 1.0 ) / ( m * m * NH2 ) );

		 // Geometrical attenuation factor
		 float G = min( 1.0, min( 2.0 * NH * NV / VH, 2.0 * NH * NL / VH ) );

		 // Fresnel term
		 float n = ( 1.0 + sqrt( F0 ) ) / ( 1.0 - sqrt( F0 ) );
		 float c = VH;
		 float g = sqrt( n * n + c * c - 1.0 );
		 float gc0 = g - c;
		 float gc1 = g + c;
		 float cgc0 = c * gc1 - 1.0;
		 float cgc1 = c * gc0 + 1.0;
		 float f0 = ( gc0 * gc0 ) / ( gc1 * gc1 );
		 float f1 = 1.0 + ( cgc0 / cgc1 ) * ( cgc0 / cgc1 );
		 float F = 0.5 * f0 * f1;

		 // Specular component
		 ds = ( F / 3.14159265 ) * ( D * G / ( NL * NV ) );
	     }

	     float Ia = ka;
	     float Id = kd * dd;
	     float Is = ks * ds;
	     return C * ( Ia + Id + Is );
	 }

	 void main()
	 {
	     vec3 C = point_color;
	     vec3 L = normalize( light_position - point_position.xyz );
	     vec3 N = normalize( normal_vector );
	     vec3 V = normalize( camera_position - point_position.xyz );

	     #if defined( Lambert )
	     vec3 shaded_color = LambertianReflection( C, L, N );
	     #elif defined( Phong )
	     vec3 shaded_color = PhongReflection( C, L, N, V );
	     #elif defined( CookTrrance )
	     vec3 shaded_color = CookTorranceReflection( C, L, N, V );
	     #else
	     vec3 shaded_color = C;
	     #endif

	     gl_FragColor = vec4( shaded_color, 1.0 );
	 }
	</script>
  <script src="Bounds.js"></script>
  <script src="Isosurfaces.js"></script>
  <script src="task1.js"></script>
  <script>main();</script>
  <script>
  function e(){
    ColorMapClick();
    ReflectionClick();
    main();
  }
  </script>
    </body>
</html>
